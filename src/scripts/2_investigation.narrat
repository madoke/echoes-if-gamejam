investigation:
  change_scene "chapter-title" next_label "sinclair_lab" title "Part 2" subtitle "Investigation"

sinclair_lab:
  talk patrick idle "Hi! Patrick Schartz, head of security, at your service."  
  talk player idle "Thanks, I'll start right away."
  think player idle "This looks excessively tidy, someone's trying to cover tracks..."
  wait 1000
  jump sinclair_lab_choices

sinclair_lab_choices:
  choice:
    "What next?"
    "Read journal" if (== $data.read_sinclair_journal false):
      set data.read_sinclair_journal true
      "...Jack is acting weird. He followed me home, and I fear he may hurt me."
      think player idle "That wasn't in the report..."
      wait 1000
      jump sinclair_lab_choices
    "Check computer" if (== $data.check_sinclair_computer false):
      set data.check_sinclair_computer true
      "The latest chips results are in. No signs of computation capabilities."
      think player idle "It looks like some emails were deleted. They must keep backups..."
      wait 1000
      jump sinclair_lab_choices
    "Search data center" if (== $data.check_sinclair_computer true):
      talk player idle "Mr. Schwartz, the restrooms?"
      talk patrick idle "First door on the right!"
      talk player idle "Thanks."
      wait 1000
      jump data_center
    "Investigate prototype wing" if (== $data.check_sinclair_computer true):
      jump revelations

data_center:
  set data.discover_horizon_plans true
  think player idle "Let'check the server backups..."
  "The latest test results are in. The new chips are AGI capable."
  "We'd like to discuss applicability to the military program."
  think player idle "They're building weapons with AI!"
  wait 1000
  jump data_center_choices

data_center_choices:
  choice:
    "What next ?"
    "Lock down":
      "Calls police station..."
      talk player idle "Illegal activities spotted at Horizon. Lock down the facility."
      talk player idle "All units to Horizon!"
      jump end_horizon_shut_down
    "Call Diaz" if (== $data.reported_to_captain_diaz false):
      set data.reported_to_captain_diaz true
      talk player idle "This is more serious than we thought, Horizon is building intelligen weapons."
      talk diaz idle "Find Veronica and stop chasing ghosts!"
      jump data_center_choices
    "Investigate prototype wing":
      jump revelations

end_horizon_shut_down:
  set_screen diaz_office
  talk diaz idle "What were you thinking?!"
  talk player idle "The evidence is compelling. We can't ignore this."
  talk diaz idle "Can't you see that I'm trying to protect us? I can't risk this department. You're off."
  wait 1000
  "Horizon denies allegations regarding a weapons program."
  "Supporting the police in finding Dr. Sinclair remains our priority."
  wait 1000
  jump end
